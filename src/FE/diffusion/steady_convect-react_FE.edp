// Problem (P):
// b \cdot \grad u + \mu \cdot u = f   en \Omega,
// u = 0   sobre \partial\Omega^- = \{ x \in \partial\Omega, b(x)\cdot n(x) <0 \}
//
// We take:
//   * \Omega= unit circle,
//   * b(x1,x2) = [x2, -x1], then \Omega^- = empty
//   * mu = 2A, A=10, x0=0.3, y0=0.3 (e.g.)
//   * Then u(x,y) = exp( -A*( (x-x0)^2 + (y-y0)^2 ) )  is an exact solution of
//     problem (P) with mu = 2*A and f(x,y) = -2*A(x*y0 - x0*y - 1)*u

//
// Global data
//
verbosity=1; // Do not let FreeFem++ show verbose info

// Exact solution
real A=10, x0=0.3, y0=0.3;
real mu=2*A;
real conv=1e5;
func uExac = exp( -A*( (x-x0)^2 + (y-y0)^2 ) );
func f = -2*A*(conv * x*y0 - conv * x0*y - 1)*uExac(x,y);

// Errors
real errL2, errH1, errL2old, errH1old;
bool computeOrder=false; // Do not compute orders at first iteration
bool saveErrors=true;
ofstream fErrorL2("erroresL2.txt");
ofstream fErrorH1("erroresH1.txt");

// Numerical approximation parameters
real eta = 1;
real normaLinfgradb = 1;

// For plotting
load "iovtk";
int [int] vtkOrder = [1];
string vtkBaseFile = "output_steady_convect_react";
real [int] viso=[-0.1,0,0.5,0.1,0.5,0.2,0.25,0.3,0.35,0.4,0.45,0.5,0.55,0.6,0.65,0.7,0.75,0.8,0.9,1];

// Macros..........................................................................
macro grad(u) [dx(u), dy(u) ] // EOM
  ; // for emacs
macro dot(u,v) ( u[0]*v[0] + u[1]*v[1] ) // EOM
  ; // for emacs
macro dotN(u) dot(u, [N.x, N.y]) // EOM
  ; // for emacs
macro pos(u) (0.5*(abs(u)+u)) // Postivie part
  ; // for emacs
macro neg(u) (0.5*(abs(u)-u)) // Negative part
  ; // for emacs
macro intInteriorEdges(expr) ( intalledges(Th)( (expr) * (nTonEdge-1)/nTonEdge) ) // EOM
  ; // for emacs
//................................................................................


//
// Actual function for solving problem
//

func int solvePDE( int nx ) {

  // Solve PDE in a mesh discretized by nx boundary intervals

  // Mesh and finite element spaces
  border Gamma(t=0, 2*pi) { x=cos(t);  y=sin(t); };
  mesh Th = buildmesh(Gamma(nx));

  fespace Uh(Th, P1);
  Uh u, ub; // Unknown and test function
  Uh b1 = conv * y, b2 = -conv * x;
  macro b() [b1, b2] // EOM
    ; // emacs

  // Plot exact solution...
  Uh uEX;
  uEX = uExac;
  plot(uEX, fill=1, wait=1, value=1, dim=3, ShowAxes = 0, viso=viso,
       cmm="Exact solution");

  // ------------------------------------------------------------------------------------
  // Finite element approximation
  // ====================================================================================
  problem ConvectReact(u,ub)=
    int2d(Th)( dot(b, grad(u)) * ub // Convection term
	       + mu * u*ub // Reaction term
	       )
    //  - int1d(Th,C)(neg(dotN(b))*u*ub)  // unused because cc=0 on d(Omega)^-
    - int2d(Th)( f(x,y)*ub );
  // ------------------------------------------------------------------------------------

  // // Problem taken from FreeFem++ doc
  // macro n()(N.x*b1+N.y*b2) //
  // problem  Adual(u,ub) = int2d(Th)((mu*u+(b1*dx(u)+b2*dy(u)))*ub)
  //   + intalledges(Th)((1-nTonEdge)*ub*(0.5*abs(n)-n/2)*jump(u))
  // //  - int1d(Th,C)((n(u)<0)*abs(n(u))*cc*w)  // unused because cc=0 on d(Omega)^-
  //   - int2d(Th)(f*ub);

  // Solve problem with centered fluxes
  //-----------------------------------
  ConvectReact;
  plot(u, fill=1, wait=1, value=1, dim=3, ShowAxes = 0, viso=viso,
       cmm="Approximate solution");
  ub = u - uExac;
  real errL2 = sqrt(int2d(Th)(ub*ub));
  cout << "errL2  = " << errL2 << endl;
  real errH1 = sqrt(int2d(Th)(ub*ub) + int2d(Th)(dot(grad(ub),grad(ub))));
  cout << "errH1 = " << errH1 << endl;

  if(saveErrors){
  fErrorL2 << errL2 << endl;
  fErrorH1  << errH1 << endl;
  }

  Uh uCF;
  uCF = u; // Save for future save to VTK

  if(computeOrder) {
    real orderL2 = log(errL2old/errL2)/log(2.); // log_2( e_h / e_(h/2) )
    cout << "OrderL2  = " << orderL2 << endl;
    real orderH1 = log(errH1old/errH1)/log(2.);
    cout << "OrderH1  = " << orderH1 << endl;
  }
  else computeOrder = true; // Compute orders from next iteration
  errL2old = errL2;
  errH1old = errH1;

  // Save to vtk for high quality plotting
  //--------------------------------------
  //string vtkFileName = vtkBaseFile + "_n=" + nx + ".vtk";
  //savevtk(vtkFileName, Th, uEX, uCF, u, dataname="u_Exact, u_CF, u_UPW", order=vtkOrder);

}

// Main program
int maxExp=10;
for (int i=4; i<maxExp+1; i++) {
  int n=2^i;
  cout << "------------------------------------------------------" << endl;
  cout << "Solving with_ n=" << n << endl;
  cout << "------------------------------------------------------" << endl;
  solvePDE(n);
 }
